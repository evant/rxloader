buildscript {
    repositories {
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

apply plugin: 'propdeps'

repositories {
    mavenLocal()
}

dependencies {
    compile 'com.netflix.rxjava:rxjava-android:0.20.4'
    provided 'android:android:4.3_r2'
    provided 'android.support:compatibility-v4:21.0.0'
}

group = 'me.tatarka.rxloader'
version = '1.0.2'

sourceCompatibility=1.6

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

if (project.hasProperty('sonatype.username') && project.hasProperty('sonatype.password'))
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { deployment -> signing.signPom(deployment) }

                def repoUrl
                if (project.version.endsWith("SNAPSHOT")) {
                    repoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                } else {
                    repoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                }

                repository(url: repoUrl) {
                    authentication(
                            userName: project.getProperty('sonatype.username'),
                            password: project.getProperty('sonatype.password'))
                }

                pom.project {
                    name 'rxloader'
                    packaging 'jar'
                    description 'Handles the activity lifecyle for rxjava\'s Observable'
                    url 'https://github.com/evant/rxloader'

                    scm {
                        url 'git@github.com:evant/rxloader.git'
                        connection 'scm:git:git@github.com:evant/rxloader.git'
                        developerConnection 'scm:git:git@github.com:evant/rxloader.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'evant'
                            name 'Evan Tatarka'
                        }
                    }
                }
            }
        }
    }
